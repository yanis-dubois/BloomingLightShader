#extension GL_ARB_explicit_attrib_location : enable

// includes
#include "/lib/common.glsl"
#include "/lib/utils.glsl"
#include "/lib/space_conversion.glsl"
#include "/lib/light_color.glsl"
#include "/lib/blur.glsl"
#include "/lib/bloom.glsl"
#include "/lib/depth_of_field.glsl"

// mipmap bloom
#if BLOOM_TYPE > 1
    const bool colortex4MipmapEnabled = true;
#endif

// textures
uniform sampler2D colortex0; // color
uniform sampler2D colortex4; // bloom
uniform sampler2D colortex5; // depth of field

// attributes
in vec2 uv;

// results
/* RENDERTARGETS: 0 */
layout(location = 0) out vec4 colorData;

/*******************************************/
/**** bloom & depth of field : 2nd pass ****/
/*******************************************/
void main() {

    vec3 color = SRGBtoLinear(texture2D(colortex0, uv).rgb);

    // -- depth of field : 2nd pass + apply it -- //
    #if DOF_TYPE > 0
        vec4 depthOfFieldData = vec4(0.0);
        vec3 DOF = doDepthOfField(uv, colortex0, colortex5, DOF_RANGE, DOF_RESOLUTION, DOF_STD, DOF_KERNEL == 1, false, depthOfFieldData);
        color = DOF;
    #endif

    // -- bloom : 2nd pass + apply it -- //
    #if BLOOM_TYPE == 1
        vec3 bloom = doBlur(uv, colortex4, BLOOM_OLD_RANGE, BLOOM_OLD_RESOLUTION, BLOOM_OLD_STD, BLOOM_OLD_KERNEL == 1, BLOOM_DITHERING_TYPE, false);
    #elif BLOOM_TYPE == 2
        vec4 bloom = doBloom(uv, colortex4, false);
        // classic bloom
        bloom.rgb = pow(bloom.rgb, vec3(0.55));
        // sun bloom
        vec3 sunBloomColor = mix(bloomSunLight, saturate(bloomSunLight, 0.75), min(rainStrength + isEyeInWater, 1.0));
        bloom.rgb += sunBloomColor * pow(bloom.a, 0.66);
    #endif
    #if BLOOM_TYPE > 0
        color += bloom.rgb * BLOOM_FACTOR;
    #endif

    colorData = vec4(linearToSRGB(color), 1.0);
}
